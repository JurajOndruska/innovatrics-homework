
/*
 * Section: Configuration of Gradle Plugins
 */
apply plugin: "application"

/*
 * Section: Maven Coordinates
 */
group = "jon.innovatrics.homework"
archivesBaseName = "runner"

dependencies {

    compile project(":XmlInput") 
    compile project(":TaskManager")
    runtime project(":TaskManagerImplementation")
    runtime project(":XmlInputImplementation")
    runtime project(":CommandExecImplementation")
}

/*
 * Section: Customization of the application plugin
 */
applicationName = "homework"
mainClassName = "jon.innovatrics.homework.runner.RunnerApplication"

/*
 * Customize JAR packaging
 */
jar {
    manifest {
        attributes (
            "Main-Class": "${mainClassName}"
        )
    }
}

/*
 * Custom task: The run the demo task
 */
task runDemo(type: JavaExec) {

    String xmlFile
    String homeDirectory

    homeDirectory = project.parent.projectDir.toString()
    xmlFile = project.parent.projectDir.toPath().resolve("Assignment/windows_processes.xml").normalize().toFile().toString()
    
    dependsOn build
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
    main = "${mainClassName}"
    args "${xmlFile}"
    jvmArgs += ["-Djon.cmdexec.subst.homeDir=${homeDirectory}"]
}
